Перем КаталогиКэша;
Перем МассивИсключений;

// Добавляет дату и время перед сообщением и выводит сообщение пользователю.
//
// Параметры:
//  ТекстСообщенияПользователю - Строка - Текст сообщения.
//
Процедура СообщитьПользователю(ТекстСообщенияПользователю)

	ТекстСообщенияПользователю = СтрШаблон("%1 %2", ТекущаяДата(), ТекстСообщенияПользователю);
	Сообщить(ТекстСообщенияПользователю);

КонецПроцедуры

// Перемещаят найденные по маскам файлы в каталог резервных копий.
//
Процедура ОчиститьКэш()
 	
    Попытка
        COMОбъект = Новый COMОбъект("Wscript.Shell");
		ИмяПапкиПользователя = COMОбъект.ExpandEnvironmentStrings("%USERPROFILE%");
	Исключение
        СообщитьПользователю(СтрШаблон(НСтр("ru = 'Не удалось получить папку пользователя:
            |%1'"), ИнформацияОбОшибке()));
        Возврат;
    КонецПопытки; 

	Для Каждого КаталогКэша Из КаталогиКэша Цикл
		
		Путь = СтрЗаменить(КаталогКэша, "%USERPROFILE%", ИмяПапкиПользователя);
		
		Сообщить("----------------------------------------------------------------------");
		Сообщить(СтрШаблон(НСтр("ru = 'Обработка каталога %1.'"), Путь));

		КаталогОбъект = Новый Файл(Путь);
		Если НЕ КаталогОбъект.Существует() Тогда
			СообщитьПользователю(НСтр("ru = 'Каталог не найден.'"));
			Продолжить;
		КонецЕсли;

		МассивФайлов = НайтиФайлы(Путь, "*", Ложь); 
		Для Каждого НайденныйФайл Из МассивФайлов Цикл
			Если НайденныйФайл.ЭтоКаталог() 
			   И МассивИсключений.Найти(НайденныйФайл.Имя) = Неопределено Тогда
				Попытка
					УдалитьФайлы(НайденныйФайл.ПолноеИмя);
				Исключение
					СообщитьПользователю(СтрШаблон(НСтр("ru = 'Не удалось удалить каталог %1:
						|%2'"), НайденныйФайл.Имя, ИнформацияОбОшибке()));
					Продолжить;
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
			
	КонецЦикла;
	
КонецПроцедуры

МассивИсключений = Новый Массив;
МассивИсключений.Добавить("ExtCompT");

КаталогиКэша = Новый Массив;
КаталогиКэша.Добавить("%USERPROFILE%\Local Settings\Application Data\1C\1Cv8");
КаталогиКэша.Добавить("%USERPROFILE%\Application Data\1C\1Cv8");
КаталогиКэша.Добавить("%USERPROFILE%\Local Settings\Application Data\1C\1Cv82");
КаталогиКэша.Добавить("%USERPROFILE%\Application Data\1C\1Cv82");

ОчиститьКэш();